# Copyright (C) 2012  Olga Yakovleva <yakovleva.o.v@gmail.com>

# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU Lesser General Public License as published by
# the Free Software Foundation, either version 2.1 of the License, or
# (at your option) any later version.

# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Lesser General Public License for more details.

# You should have received a copy of the GNU Lesser General Public License
# along with this program.  If not, see <https://www.gnu.org/licenses/>.

define NZDigit 1|2|3|4|5|6|7|8|9;
define Digit %0|NZDigit;
define Number %0|[NZDigit Digit^<15];

define InsertThousands 
[..] -> mil || Digit _ Digit^3 .#.;

define InsertMillions 
[..] -> miliono || Digit _ Digit^3 mil;

define InsertBillions 
[..] -> miliardo || Digit _ Digit^3 miliono;

define InsertTrillions 
[..] -> biliono || Digit _ Digit^3 miliardo;

define RemoveSkippedParts 
%0 %0 %0 [mil|miliono|miliardo|biliono] -> 0 ;

define Units %0:0|1:unu|2:du|3:tri|4:kvar|5:kvin|6:ses|7:sep|8:ok|9:naŭ;

define Tens %0:0|1:dek|2:dudek|3:tridek|4:kvardek|5:kvindek|6:sesdek|7:sepdek|8:okdek|9:naŭdek;

define Hundreds %0:0|1:cent|2:ducent|3:tricent|4:kvarcent|5:kvincent|6:sescent|7:sepcent|8:okcent|9:naŭcent;

define UpToThousand %0:nulo .P. [((Hundreds) Tens) Units];

define DigitsToWords 
UpToThousand @->;

regex 
Number .o. 
InsertThousands .o. 
InsertMillions .o. 
InsertBillions .o. 
InsertTrillions .o. 
RemoveSkippedParts .o. 
DigitsToWords ; 
