# Copyright (C) 2016, 2018  Olga Yakovleva <yakovleva.o.v@gmail.com>

# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU Lesser General Public License as published by
# the Free Software Foundation, either version 2.1 of the License, or
# (at your option) any later version.

# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Lesser General Public License for more details.

# You should have received a copy of the GNU Lesser General Public License
# along with this program.  If not, see <https://www.gnu.org/licenses/>.

define Consonant 
б|г|д|ж|з|к|л|м|н|п|р|с|т|ф|х|ц|ч|ш|щ|й|в|ң|ӊ|Б|Г|Д|Ж|З|К|Л|М|Н|П|Р|С|Т|Ф|Х|Ц|Ч|Ш|Щ|Й|В|Ң|Ӊ | 
b|c|d|f|g|h|j|k|l|m|n|p|r|s|t|q|v|w|x|z | 
B|C|D|F|G|H|J|K|L|M|N|P|R|S|T|Q|V|W|X|Z ; 

define Vowel 
а|е|ё|и|о|у|ю|я|ы|э|ө|ѳ|ɵ|ү|А|Е|Ё|И|О|У|Ю|Я|Ы|Э|Ө|Ѳ|Ɵ|Ү|Υ | 
a|e|i|o|u|y | 
A|E|I|O|U|Y ; 

define Letter 
Consonant|Vowel|ь|ъ;

define Word 
Letter+ ;

define NZDigit 1|2|3|4|5|6|7|8|9;
define Digit %0|NZDigit;

define Number %0 | [NZDigit Digit*];

define ConsonantSequence 
Consonant+ ;

define LetterSequence 
ConsonantSequence | [ь|ъ]+ ;

define BasicTokenizer 
Word @-> ... word || _ ,, 
NZDigit Digit* @-> ... num ,, 
%0 Digit+ @-> ... dig ,, 
%0 @-> ... num ,, 
\[Letter | Digit] @-> ... sym ; 

define TagLetterSequences 
word -> lseq || [.#.|word|num|dig|sym] LetterSequence _ ; 

define UntagLargeNumbers 
num -> dig || Digit^13 _ ; 

define SplitDigitSequences 
[[%0 Digit 0:dig] | [%0 0:num]] [[NZDigit Digit 0:num] | [%0 Digit 0:dig]]* dig:0 @-> ; 

define TagSigns 
sym -> 0 || [%+|%-] _ Digit+ num ,, 
num %% sym -> %% num ; 

regex 
BasicTokenizer .o. 
SplitDigitSequences .o. 
TagLetterSequences .o. 
UntagLargeNumbers .o. 
TagSigns ; 
