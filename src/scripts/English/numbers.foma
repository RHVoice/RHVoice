# Copyright (C) 2012, 2014  Olga Yakovleva <yakovleva.o.v@gmail.com>

# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU Lesser General Public License as published by
# the Free Software Foundation, either version 2.1 of the License, or
# (at your option) any later version.

# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Lesser General Public License for more details.

# You should have received a copy of the GNU Lesser General Public License
# along with this program.  If not, see <https://www.gnu.org/licenses/>.

define NZDigit 1|2|3|4|5|6|7|8|9;
define Digit %0|NZDigit;

define RB ({st}|{nd}|{rd}|{th}) .#.;

define UnformattedCardinalNumber %0|[NZDigit Digit^<15] ; 
define FormattedCardinalNumber NZDigit Digit^<3 [%, Digit^3]^{1,4} ; 
define CardinalNumber UnformattedCardinalNumber | FormattedCardinalNumber ; 

define OrdinalNumber 
[[[CardinalNumber & [?* 1]] - [?* 1 1]] {st}] | 
[[[CardinalNumber & [?* 2]] - [?* 1 2]] {nd}] | 
[[[CardinalNumber & [?* 3]] - [?* 1 3]] {rd}] | 
[[CardinalNumber & [?* [%0|4|5|6|7|8|9|{11}|{12}|{13}]]] {th}] ; 

define Number CardinalNumber | OrdinalNumber ; 

define RemoveThousandsSeparator 
%, -> 0 ; 

define InsertThousands 
[..] -> thousand || Digit _ Digit^3 RB.;

define InsertMillions 
[..] -> million || Digit _ Digit^3 thousand;

define InsertBillions 
[..] -> billion || Digit _ Digit^3 million;

define InsertTrillions 
[..] -> trillion || Digit _ Digit^3 billion;

define RemoveSkippedParts 
%0 %0 %0 [thousand|million|billion|trillion] -> 0 ;

define InsertHundreds 
[..] -> hundred || NZDigit _ Digit^2 ,, 
%0 -> 0 || _ Digit^2 ;

define Units %0:0|1:one|2:two|3:three|4:four|5:five|6:six|7:seven|8:eight|9:nine;

define Teens 1:0 [%0:ten|1:eleven|2:twelve|3:thirteen|4:fourteen|5:fifteen|6:sixteen|7:seventeen|8:eighteen|9:nineteen];

define Tens %0:0|2:twenty|3:thirty|4:forty|5:fifty|6:sixty|7:seventy|8:eighty|9:ninety;

define UpToHundred %0:zero .P. [Teens | [(Tens) Units]];

define DigitsToWords 
UpToHundred @-> ; 

define CardinalsToOrdinals 
[zero : zeroth] | 
[one : first] | 
[two : second] | 
[three : third] | 
[four : fourth] | 
[five : fifth] | 
[six : sixth] | 
[seven : seventh] | 
[eight : eighth] | 
[nine : ninth] | 
[ten : tenth] | 
[eleven : eleventh] | 
[twelve : twelfth] | 
[thirteen : thirteenth] | 
[fourteen : fourteenth] | 
[fifteen : fifteenth] | 
[sixteen : sixteenth] | 
[seventeen : seventeenth] | 
[eighteen : eighteenth] | 
[nineteen : nineteenth] | 
[twenty : twentieth] | 
[thirty : thirtieth] | 
[forty : fortieth] | 
[fifty : fiftieth] | 
[sixty : sixtieth] | 
[seventy : seventieth] | 
[eighty : eightieth] | 
[ninety : ninetieth] | 
[hundred : hundredth] | 
[thousand : thousandth] | 
[million : millionth] | 
[billion : billionth] | 
[trillion : trillionth] ; 

define FixOrdinals 
CardinalsToOrdinals [[{st}|{nd}|{rd}|{th}]:0] -> || _ .#. ; 

regex 
Number .o. 
RemoveThousandsSeparator .o. 
InsertThousands .o. 
InsertMillions .o. 
InsertBillions .o. 
InsertTrillions .o. 
RemoveSkippedParts .o. 
InsertHundreds .o. 
DigitsToWords .o. 
FixOrdinals ; 
