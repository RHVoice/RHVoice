# Copyright (C) 2021 Grzegorz Zlotowicz  <grzezlo@gmail.com>

# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU Lesser General Public License as published by
# the Free Software Foundation, either version 2.1 of the License, or
# (at your option) any later version.

# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Lesser General Public License for more details.

# You should have received a copy of the GNU Lesser General Public License
# along with this program.  If not, see <https://www.gnu.org/licenses/>.

define InitialTranscribe 
a:a | 
ą:on | 
b:b | 
c:c | 
{ch}:h | 
{cz}:ch |
ć:ci | 
d:d | 
{dz}:dz |
{dź}:dzi |
{dż}: j |
e:e | 
ę:en | 
f:f | 
g:g | 
h:h | 
i:i | 
j:y | 
k:k | 
l:l | 
ł:w | 
m:m | 
n:n | 
{ni}: ni |
ń:ni | 
o:o | 
ó:u | 
p:p | 
q:{ku} | 
r:r | 
{rz}: zh |
s:s | 
{sz}:sh | 
ś:si | 
t:t | 
u:u | 
w:v | 
v:v | 
x:{ks} | 
y:ih | 
z:z | 
zi:zi | 
ź:zi |
ż: zh ;

define Stage1 InitialTranscribe @-> ;

define VowelPhonem [a | on | e | en | i | o | u | ih ] ;
define ConsonantPhonem [b|c|ch|ci|d|dz|dzi|j|f|g|h|y|k|l|w|m|n|ni|p|r|s|sh|si|t|v|z|zi|zh] ;
define SofteningConsonantPhonem [c|h|k|p|s|sh|si|t] ;
define SofteningWZ [v:f | z: s ] ;

define Stage2 
zh @-> sh || [h|k|p|t] _ ,, 
SofteningWZ @-> || SofteningConsonantPhonem _ ,,
SofteningWZ @-> || _ SofteningConsonantPhonem ,,
i @-> 0 || n _ VowelPhonem ,,
[{ci}: ci | 
[dz i]: dzi ] @->  || _ VowelPhonem ,,
[{ci}: [ci i] | 
[dz i]: [dzi i] ] @-> || _ ConsonantPhonem ,,
i @-> y || _ VowelPhonem ,,
[on : o |
en: e] @-> || _ w ;


regex Stage1 .o.
Stage2 ;
