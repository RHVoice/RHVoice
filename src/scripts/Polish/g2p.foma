# Copyright (C) 2021 - 2022 Grzegorz Zlotowicz  <grzezlo@gmail.com>

# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU Lesser General Public License as published by
# the Free Software Foundation, either version 2.1 of the License, or
# (at your option) any later version.

# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Lesser General Public License for more details.

# You should have received a copy of the GNU Lesser General Public License
# along with this program.  If not, see <https://www.gnu.org/licenses/>.

define InitialTranscribe 
a:a | 
ą:on | 
b:b | 
c:c | 
{ch}:h | 
{cz}:ch |
ć:ci | 
d:d | 
{dz}:dz |
{dź}:dzi |
e:e | 
ę:en | 
f:f | 
g:g | 
h:h | 
i:i | 
j:y | 
k:k | 
l:l | 
ł:w | 
m:m | 
n:n | 
ń:ni | 
o:o | 
ó:u | 
p:p | 
r:r | 
s:s | 
{sz}:sh | 
ś:si | 
t:t | 
u:u | 
w:v | 
v:v2 | 
y:ih | 
z:z | 
ź:zi ;
#q and ż processed later.

define Stage1 InitialTranscribe @-> ;

define VowelPhonem [a | on | e | en | i | o | u | ih ] ;
define ConsonantPhonem [b|c|ch|ci|d|dz|dzi|dzh|f|g|h|y|k|l|w|m|n|ni|p|r|s|sh|si|t|v|z|zi|zh] ;

define processMultigraphs1 
#Convert rz to virtual "rz" phonem, later backconverted to r and z.
{rz} @-> rz ||
#zamarza, marzli, marzł, marznie...
{ma} _ a|l|w|n|ni ,
#mierzi, obmierzł, zmierzły, not mierzy, mierz...
m i e _ i|w,
#erzac, nie rycerzach, nie domierzacie:
e _ a c[ \i|.#.],
#erzatz:
e _ a t z ,
#superzdrowy, hiperzręczny, cyberzagrożenia:
{supe}| {hipe} | [c  ih b e] _ ,,
#d ż, nie dż:
[d ż] @-> dzh2 ||
#budże*
{bu} _ e,
#nadżarcie, nadżerka, podżartować:
{na}|{po} _ [a|e] r,
#podżarcie, podżółkły, podżuchwowa, podżyrować:
{po} _ [a|e|u|ih],
#odżywić, nieodżałowany, niewspółodżywiony, współoskarżyć
.#. (n i e) (v s p u w) o _ VowelPhonem ;


define processMultigraphs2 
#Convert rest of "rz" to zh phonem:
{rz} -> zh ||  _ ,,
#convert to zh also exceptions from virtual rz
rz -> zh ||
#umarza, umarzalność...
{uma} _ a ,
#marzanna:
{ma} _ {ann} ,
#dymarza
d ih m a _ a ,
#kałamarza:
k a w a m a _ a,
#karczmarza:
k a r ch m a _ a,
#kramarza:
k r a m a _ a,
#marzanowata:
m a _ a n o ,
#komarza, przekomarzać nieprzekomarzać:
k o m a _ a,
#rymarza:
r ih m a _ a,,
#dż to dzh:
[d ż] -> dzh || _ ,,
#exceptions from dzh2:
dzh2 -> dzh ||
#menadżer:
m e n a _ e r ;

define processMultigraphs3 
#Convert virtual rz from multigraphs1 to r z phonemes:
rz @-> r z || _ ,,
#dzh2 to d zh:
dzh2 @-> d zh || _ ;

define SofteningConsonantPhonem [c|ch|f|h|k|p|s|sh|si|t] ;
define Softening [
v:f|
z: s|
zh:sh] ;

define FlagExceptions 
#antyatom, antyenergetyczny, antyimport, antyoksydant, antyustrojowy:
ih @-> ih2 || {ant} _ VowelPhonem,,
#d z, not dz:
[dz] @-> dz2 ||
#odzew,  nieodzowny, odzmysławiać, odznaka, odzywać...
.#. (n i e) o _ \i ,
#nadzorca, nadzwyczaj, not [nadzie nadze nadzy ]
{na} _ \[e|i|ih],
#podzakres, podzamcze, podzamkowy:
p o _ a [k|m],
#podzwrotnikowy
p o _ v r,
#podzelować, podzespół:
p o _ e ,,
#d zi, not dzi:
[dz i] @-> dzi2 ||
#odziomek
.#. o _ o ,
#podziemię, nadziemny, niepodziemny:
.#. (n i e) [p o] | [n a] _ e m,
#podzięb:
.#. (n i e) p o _ en b ,,
#d zi i not dzi
[dz i] @-> dzi3 ||
#odzipnąć
.#. o _ p ,,
#si not converted to "si":
{si} @-> si2 ||
#sigm, sign, siglum, pleksiglas:
_ g [l|m|n],
#silikon, silicon, not silnik:
_ l i [c|k],
#sinus, singiel, single, sinolog, not sina, sinego, sinów...:
_ n [ g |{olo} | [ u s ]],
#aksis aksisie not basista, not nosiska
 _ s [.#.|\[t|k]],
 #eliksir
 k _ r,
#epsilon, ypsilon
p _ l ,
#impossible, not aeropressie
s _ \e,
#pepsi:
p e p _ ,
#sim, not psim, not cieplusimi
.#. _ m,
#sizal, sizalami, not bruksizm, marksizować, psiząb...
_ z [\on&\m&\o],,
#"z i", not convert to zi:
{zi} @-> zi2 ||
#*bezi (not bezik*)
.#. [u | n i e | 0] b e _ \k,
#dezinstalacja and similar, not artrodezie
d e _ \e,
#dżazira, nazir, not kazir*
[\k] a _ r,
#mozilla:
_ l l,
#muezin, muezzin:
m u e (z) _ n,
#niezidentyfikowany, niezintegrowany,  niezilustrowany 
.#. (n i e) _ [d|l|n],
#rozigrane, nieroziskrzone, 
.#. (n i e) r o _ ,
#Zanzibar,
n _ b ,,
#Not soften zh:
#xie na ksie:
x i e @-> k si e || _ ,,
#quasi:
q u a s i @-> k f a zi2 || _ ;


define UnflagExceptions [
dz2:[d z] |
dzi2:[d zi] |
dzi3:[d zi i] |
dzh2:[d zh] |
si2: [s i] |
v2 : v |
ih2: ih |
zi2:[z i] |
zh2:zh ] @-> || _ ;

define Stage2 
Softening @-> || SofteningConsonantPhonem _ ,
 _ SofteningConsonantPhonem,
 \.#. _ .#. ,,
 #Before vowel:
[{ci}: ci | 
{ni}: ni | 
{si}: si | 
{zi}: zi | 
[dz i]: dzi ] @->  || _ VowelPhonem ,,
#Before consonant or word boundary:
[{ci}: [ci i] | 
{ni}: [ni i] | 
{si}: [si i] | 
[dz i]: [dzi i] ] @-> || _ [\VowelPhonem | .#. ],,
#"zi" to z i or "zi i":
#in the middle of word
{zi} @-> [zi i] || \.#. _ \VowelPhonem|.#. ,
#zim, zima, zimie, zimo, zimu, zimy:
.#. (n i e) _ m [.#.|a|on|en|i|n|o|u|ih],
#zip*
.#. _ p,,
#e.g. historia to historja:
i @-> y || _ VowelPhonem ,,
#e.g.: zaczął=zaczoł, zaczęła=zaczeła:
[on : o |
en: e] @-> || _ w ,,
#klient:
k l i e @-> k l i y e || _ ,,
#statua:
u a @-> u w a || _ .#. ,,
#statuy:
u ih @-> u w ih || _ .#. ,,
# nadzerowy pominięte  wcześniej przez flagexceptions:
dz @-> dz2  || n a _ e r o ,,
#q na ku:
q @-> {ku} || _ ,,
#ż na zh
ż @-> zh || _ ,,
#x na ks:
x @-> k s || _ ;

source stress.foma
source unicodechars.foma

regex UnicodeToNativeTranslit .o.
Stage1 .o.
processMultigraphs1 .o.
processMultigraphs2 .o.
processMultigraphs3 .o.
FlagExceptions .o.
Stage2 .o.
UnflagExceptions .o.
procStressG2p .o.
procStress2;
