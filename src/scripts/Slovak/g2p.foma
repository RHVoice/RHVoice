# Copyright (C) 2022 Darko Milosevic <daremc86@gmail.com>
# Copyright (C) 2023 Peter Vagner <pvagner@pvagner.tk>

# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU Lesser General Public License as published by
# the Free Software Foundation, either version 2.1 of the License, or
# (at your option) any later version.

# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Lesser General Public License for more details.

# You should have received a copy of the GNU Lesser General Public License
# along with this program.  If not, see <https://www.gnu.org/licenses/>.

define InitialTranscribe 
# Now, we should define some phonemes which represents something symillar as schwa for vocalize R and L
а:ax1 | 
и:iii1 | 
a:a | 
ä:ae | 
á:aa | 
b:b | 
c:c | 
č:ts | 
d:d | 
[d z]:dz | 
ď:dj | 
e:e | 
é:ee | 
ě:ije | 
f:f | 
g:g | 
h:h | 
i:i | 
í:ii | 
j:j | 
k:k | 
l:l | 
ľ:lj | 
ĺ:ll | 
m:m | 
n:n | 
ň:nj | 
o:o | 
ó:oo | 
ô:uo | 
ö:o | 
p:p | 
q:kv | 
r:r | 
ř:rz | 
ŕ:rr | 
s:s | 
š:sh | 
ß:s | 
t:t | 
ť:tj | 
u:u | 
ú:uu | 
ů:ou | 
ü:u | 
w:v | 
v:v | 
x:[ks] | 
y:y | 
ý:yy | 
z:z | 
ž:zh ; 

define Digraphs 
[c h]:[hh] | 
[d zh]:[dzh] | 
[z s]:[s] | 
[m ije]:[m nj e] | 
[d ije]:[dj e] | 
[t ije]:[tj e] | 
[n ije]:[nj e] | 
ije:[i e] ; 

define spellSpecialCharacters [
[ae]: [p r e h l a s o v a n ee aa] | 
[ije]: [ee z m e k ts e nj o m] |
[ou]: [uu s k r uu sh k o m] 
] @-> || .#. _ .#. ;

define processSpecialCharacters [
[ae]: [e] | 
[ou]: [uu] 
] @-> || _ ;

define ShortVowelPhonem [a | e | i | o | u | y ] ;
define LongVowelPhonem [aa | ee | ije | ii | oo | uo | uu | ou | yy | iii1 | ax1 ] ;
define VowelPhonem ShortVowelPhonem | LongVowelPhonem ;
#define ConsonantPhonem [b|c|ts|d|dj|dz|dzh|f|g|h|hh|j|k|l|lj|m|n|nj|p|r|rz|s|sh|t|tj|v|z|zh|vdp|vdt] ;
define VoicedConsonantPhonem [b|d|dj|dz|dzh|g|h|z|zh|v|m|n|nj|l|ľ|r|j|vlb|vld|vldj|vldz|vldzh|vlg|vlh|vlz|vlzh|vlv] ; 
define VoicelessConsonantPhonem [p|t|tj|c|ts|k|hh|s|sh|f|vdp|vdt|vdtj|vdc|vdts|vdk|vdhh|vds|vdsh|vdf] ; 
define ConsonantPhonem [VoicedConsonantPhonem | VoicelessConsonantPhonem | rz] ; 

define deExceptions {de} @-> [ŧde] || .#. _ {bat} | {ce} | [f i n i t ii v] | {inte} | [k aa d] | {kor} | [k oo r] | {krement} | [k r ee t] | {leg} | {lim} | {pres} | {sig} | {sti} | {tail} | {tek} | [v ii z] | z , [k o r e sh p o n] _ n 
, .#. {vi} _ a | [aa] | [ii] | o | u , {mo} _ r , {prezi} _ {nt} , .#. i _ a | [aa] | [ii] | {nti} | u , .#. {mo} _ l | m , .#. {fe} _ r | x , .#. {evi} _ n , {in} _ x , _ {ci} | [l f ii n] | [n t aa l] | {term} , i _ n | o , [sh t u] _ {nt} , .#. {ka} _ t , a _ k | l | p , _ {fin} , {je} _ n .#. | [n aa] , _ {but} | {fault} , .#. _ {mo} , [k a zh] | {mla} _ j , {po} | {pre} _ [nj] .#. , _ {gene} , _ {ment} | {menc} , {bor} _ l , _ {rby} , _ {vel} , _ [r n i ee] , {aka} _ {mic} | {mik} , _ {form} , {epi} _ {m} , {in} _ [ks] , {ten} _ {nc} | [n ts] , [zh i a] _ n .#. , [k oo] _ [ks] ;
define diExceptions [d i] @-> [ŧdi] || [aa] _ i .#. , [aa] _ a | [aa] | i | [ii] | o | u , _ {ck} | {dakti} | f | {kt} | o | [oo] | r | {sp} | {st} | {stic} | {zmus} , .#. _ [aa] | [ee] | {ec} | {et} | [f t ii n] | g | {men} | p | s | [v ii z] , .#. {au} | {diskre} | {expe} | {han} | {meto} | {mo} | {skan} | [sh t a] | [sh t aa] | [sh t i p e n] | [sh t u] | [sh t uu] _ , .#. [sh t a n d a r] _ z , {in} | {kre} | {me} | {melo} | [m e l oo] | [m ee] | {or} | {tra} _ , {man} _ t , .#. o | {pre} _ [sh] , .#. o _ n | s , .#. e _ t , .#. {invali} _ t | z , o _ c | k , _ k .#. , .#. {ra} _ [k aa l] , .#. i _ l | o , .#. {ko} _ {fi} , .#. _ a .#. | {al} , .#. _ [ts] .#. , _ {zajn} , {kan} _ d | z , .#. _ {va} .#. | {ve} .#. | [v aa m] .#. | {vou} .#. | {vami} .#. , .#. _ {sk} , _ {verz} , _ {lem} , _ {scip} , {hen} _ {kep} , _ {sku} \.#. ; 
define diiExceptions [d ii] @-> [ŧdii] || .#. {na} | o | {po} | {pre} _ d | [dj] , .#. {tra} _ , .#. {expe} _ , {mla} _ .#. , .#. {na} | .#. o | .#. {po} | .#. {pre} _ [s tj] .#. , [r aa] _ i , [uu] _ i , {kon} _ {ci} ; 
define neExceptions [n e] @-> [ŧne] || _ {gat} | [g aa c] | {nt} | {oklas} | {ore} | [oo] | {rv} | {utr} | [ks t] , .#. {be} _ f , .#. {fo} | .#. {li} | .#. [p r aa z d] _ , g | t | v _ r , [a d m i n i s t r a t ii v] | [i n t e r] | {ge} | [k r ee t] _ , {mag} | [p e r s o n aa l] | {pyre} | [r e aa l] | [r uo z] _ , [sh p e c i aa l] | [sh t aa t] | [t e hh] | {vin} | [zh i a d] _ , [s o c i aa l] | [s p r aa v] _ , e _ {rg} , .#. {re} _ s , {ba} | [t r ee] _ r , {me} _ [zh] , .#. _ g | [o p r ee n] , {pa} _ l , [aa l] | [ii z] _ {ho} , {av} | [aa v] | {ed} | {na} | t _ j .#. , _ [ts k o] .#. , .#. o | {vi} _ n .#. , [zh i a r] _ , [t ii] _ [dzh] , [m yy t] _ , {miest} _ .#. , [p aa d] _ , .#. | r _ t .#. | [t y .#.] | [t u .#.] | {tov} | [t u .#.] | {te} .#. | [t o hh] | {tmi} | {tami} , [ts] _ j .#. , [\m e] _ j .#. , i _ j .#. , o _ j .#. , ConsonantPhonem _ j .#. , [ts i e r] _ , {va} _ j .#. , [t ii v] _ {mu} .#. , _ [p aa l [e | u | s]] ; 
define niExceptions [n i] @-> ŧni || [g ee] | {mo} | u _ , [aa l] | {bota} | {endro} | {go} | [g oo] | {harmo} | [h a r m oo] _ , {kolo} | [k o l oo] | [l ii] | [m e hh a] | [oo] | [t e hh] | [t r ee] | {tro} _ , _ {kot} | {st} , .#. {tu} _ s , a _ m , {fo} _ c , [hh] _ c | k , {orga} _ c | z , {ro} _ c | k , {te} _ s , i _ {ci} , {mu} _ k | s , {tago} _ s , .#. {ge} | {imu} | {mi} _ , o _ c | k | s , {admi} _ s , _ {zmus} .#. , .#. [uu] _ a | e | i | o | u | [ii] , {defi} _ t , [f ee] _ [ks] , [j uu] _ , [aa] _ a | e | {ou} | u , [aa r] | {atki} | {iad} | [ii sh k i] _ .#. , .#. {va} _ l , _ {hilist} , {fi} _ c | t | [ts] , {ma} _ {fe} , {mu} _ [ts] , {tur} _ {ket} , [hh r o] _ z , [ts i e r] _ , [d aa v] _ , {kog} _ , {homoge} _ , {se} _ {or} , {hygie} _ , {vulka} _ {ck} , {ma} _ {pul} ;
define niiExceptions [n ii] @-> [ŧnii] || [ŧde r] _ .#. , {mu} _ c , {vole} _ , {defi} _ c , {ave} | {buz} | {ent} | {er} | {ere} | {ert} | {il} | {op} | {ot} | [ts a s] | [ts e] _ .#. , {ek} | [e ts] | {in} | {ova} | {patr} | {tat} | [u ts] | [zh e] _ .#. , .#. i _ .#. , {atki} _ .#. , .#. o _ .#. , {der} | {pred} _ .#. , {odne} _ .#. , {kog} _ , [e lj] _ .#. , [a ts] _ .#. , {ved} _ .#. , [t o ts] _ .#. ; 
define teExceptions [t e] @-> [ŧte] || _ {nis} | [r a j sh] | {restri} | [ks] , {syn} | {zavia} _ , .#. _ [d a j sh] | [hh] | {lec} | {lef} | {leg} | {lek} | {lem} | {leo} | {lep} | {ler} | {lesk} | {lesen} | {leter} | {lev} | {mp} | {nd} | {nto} | o | [oo] | {rap} | {ras} | [r ee n] | [r ee z] | [r ts] | {rez} , [e ks i s] _ n , {ho} _ l , {arak} | {arac} _ r , .#. {in} _ l | n , {kompe} _ n , _ {mati} , {ma} _ {ria} , {ma} _ [r i aa] , {stra} _ g , {ia} | [n aa s] _ .#. , {me} _ r .#. , v _ [d a j sh] , {de} _ [k t ii v] , {in} _ {rn} , {al} _ r , .#. {dein} | {pas} | [v a l] _ , [a r hh i] _ k , .#. {pa} _ {nt} , {sep} _ {mb} , [ee] _ r , {asis} _ n , [v sh e] _ [ts] , [ŧde] _ k , _ {ho} .#. | j .#. | s .#. , s _ r , .#. {ka} _ d | g , _ {raz} , {ka} _ [hh] , {al} _ [r n a t ii v] , _ {rm} | {st} , {arak} _ r , [e ks] _ r , .#. {tele} _ r , .#. {po} _ n , {in} | {li} _ r , {sys} _ m , {al} _ {rn} , v _ {dy} , {la} _ {nc} | {nt} , {en} | {fil} | {pe} | {sve} _ r , .#. _ [n .#.] | [n t o .#.] , _ {da} .#. , _ {mer} , {tam} _ n , {to} _ m | n , _ {jto} .#. , {me} _ o , .#. _ l .#. , {in} _ g , {ve} _ {rin} , _ {lk} | {rk} , _ [hh n] | [hh ŧni] | [hh ŧne] , {ve} _ [r aa n] , _ {lenov} , _ [n ŧde n c] | [n ŧde n ts] , [o ts] _ , o _ {rap} , _ [s l [a | u | e | y]] , _ [h e r aa n] , _ [ŧni s] , {sa} _ {lit} , _ [a t] | [aa t] ;
define tiExceptions [t i] @-> ŧti|| _ k .#. | {ng} .#. , _ {st} | {tul} , {abs} | {apa} | {atlan} _ , {automa} | {balis} | {cer} | {diagnos} _ , {didak} | [ŧdi d a k] | {ek} | [e ks o] | {ema} | {farmaceu} _ , {fone} | {herme} | [hh a r a k ŧte r i s] | {iden} | [i ŧde n] | {ins} | [i n sh] | {inves} | {kon} _ , [k o n sh] | {kozme} | {kul} | {magne} | [m a ŧte m a] | {mul} _ , {niko} | {op} | {par} | {produk} | {rk} _ , {sensi} | [s y n ŧte] | [sh t a] | [sh t a t i s] _ , [ŧte m a] | {ver} _ , {komple} | {konkre} | {kri} _ z , a _ z , {li} | n | {tas} | y _ c , {ama} | {ak} | {ep} | [hh a] | [hh a o] | {kri} | o | {ro} _ c | [k VowelPhonem] , _ {ck} , {sa} _ r | s , {pres} _ [zh] , s _ {mul} , .#. _ {motej} | {ran} , .#. [r e sh] _ , [t e ks] _ l , .#. {pu} _ k , {ven} _ l , {ma} _ k , e _ k , e _ {ck} , _ {zmus} .#. , {en} _ n , a _ {ck} , .#. _ {tan} , [sh] _ {pen} , {ak} | {mo} _ v , n _ n , {an} _ [k aa] , {mar} _ n , {ahnu} | {ihnu} _ .#. , .#. _ e .#. | {eto} .#. , .#. _ p .#. | {py} .#. | {pu} .#. | {pov} | {po} | {pe} .#. | {pmi} .#. , {kris} _ [aa] , {pe} _ [ts n] , _ {mea} | {mei} | [m e ii] | [m e aa m] | [m e u] | [m e aa hh] | {meou} | {meami} , .#. {an} _ , _ {ger} | {gr} , {fes} _ {val} , .#. {des} _ , .#. {jus} _ , .#. {kons} _ , .#. [a n ŧti p a] _ , {poli} _ c | k , .#. {ak} _ , {cen} _ \ShortVowelPhonem , {empa} _ , .#. {in} _ m , e _ [oo p] , {marke} _ {ngo} , [ŧde s] _ l , s _ {gm} ; 
define tiiExceptions [t ii] @-> [ŧtii] || {preven} _ v , {detek} | {dif} | {ek} | {sensi} | {viknu} _ , {teris} _ k , _ {mov} , a | {ak} | {alterna} | {audi} | {defini} | i | [k o n sh t r u k] | {mo} | {nega} | {produk} | {spek} _ v , .#. {jus} | {pales} _ , {or} _ m , {ales} _ t , .#. _ [m .#.] | {mo} | {mu} , {os} _ k , {hnu} | {ja} | {li} _ .#. , .#. _ .#. | {to} .#. , {kris} _ n , _ [ŧne dzh] , {pe} _ {ci} , [ŧni] _ , _ {mea} | {mei} | [m e ii] | [m e aa m] | [m e u] | [m e aa hh] | {meou} | {meami} , .#. _ m , {legi} _ m , {adap} _ v , .#. {in} _ m , [ŧde ŧte k] _ v ;
define rules [d i]:[dj i] | [t i]:[tj i] | [n i]:[nj i] | [l ii]:[lj ii] | [l i]:[lj i] | [d e]:[dj e] | [t e]:[tj e] | [n e]:[nj e] | [l e]:[lj e] | [d ii]:[dj ii] | [n ii]:[nj ii] | [t ii]:[tj ii] ; 
define exceptionsEnd [ŧde] @-> [d e] || _ ,, [ŧdii] @-> [d ii] || _ ,, [ŧdi] @-> [d i] || _ ,, [ŧne] @-> [n e] || _ ,, [ŧnii] @-> [n ii] || _ ,, [ŧni] @-> [n i] || _ ,, [ŧte] @-> [t e] || _ ,, [ŧtii] @-> [t ii] || _ ,, [ŧti] @-> [t i] || _ ;

define GeneralPrefix {bez} | {do} | [d uo] | [m i m o] | {na} | {nad} | [n aa] | [nj e] | [nj e n a] | o | {od} | {odo} | {po} | [vdp o] | {pol} | [vdp o l] | {pre} | [vdp r e] | {pri} | [vdp r i] | {roz} | s | {so} | [sh] | {tri} | [vdt r i] | u | {uza} | v | {vy} | {vyna} | {za} | [z aa] ; 
define sPrefix {popri} | [vdp o vdp r i] | {pre} | [vdp r e] | {pri} | [vdp r i] | {za} | [z aa] ;
define AdjectiveSuffix [n aa] | {ne} | [nj e] | {ny} | [n yy] | {ni} | [nj i] | [n ii] | [nj ii] | {neho} | [nj e h o] | [y hh] | {nemu} | [nj e m u] | [n ee m u] | {nym} | {no} | {nom} | [n yy m] | {nimi} | [nj i m i] | [n ii m i] | [nj ii m i] | {nej} | [nj e j] | [n ee] | [n ee h o] | {mi} | [nj ii k] | [nj ii vdk] | [n uu tj] | [n y hh] | [n yy hh] | [yy mi] | [nj o v] | [n o s tj] | [n o s tj i] | [n o s tj ii] | [n o s tj i a m] | [n o s tj i a hh] | [n o s tj o u] | [n o s tj a m i] | [n uu] ;
define VSuffix [o v a tj] | [a tj] | [aa tj] | {ujem} | {ujeme} | [u j e sh] | [u j e tj e] | {oval} | {ovala} | {ovalo} | {ujeme} | [u j e tj e] | [u j uu] | {ame} | {am} | [a sh] | [a tj e] | a | {ame} | [a tj e] | [a j uu] | {aj} | [o r i tj] | {orka} | {il} | [i lj i] | {ila} | {ilo} | {ina} | {inka} | {iny} | {inky} | [ii n] | [i nj i e k] | [i nj e] | {inke} | [i n aa m] | [i n k aa m] | {inu} | {inku} | {iny} | {inky} | [i nj e] | {inke} | [i n aa hh] | [i n k aa hh] | {inou} | {inkou} | {inami} | {inkami} | {ice} | {iciam} | [i c i a hh] | {icami} | [ii c] | [nj ii k y] | [nj ii k a] | [nj ii k o v] | [v nj ii k u] | [nj ii k o m] | [nj ii k o hh] | [nj ii k a m i] | [nj ii k m i] | [nj ii k] | [nj ii vdk] | {la} | {ala} | [nj a] | {ov} | [a n yy] | [a n aa] | [a n ee] | [a n ii] | [a n ee h o] | {anej} | [a nj e j] | [a n ee m u] | [a n yy m] | [a n uu] | [a n yy hh] | {anom} | [a n yy hh] | [a n yy m i] | [nj u j e m] | [nj u j e sh] | [nj u j e] | [nj u j e m e] | [nj u j e tj e] | [nj u j uu] | {ner} | {neri} | {nera} | {nerov} | {nerovi} | {nerom} | {nera} | [n e r o hh] | {nerami} {nermi} | {na} | [nj i k] | [nj i c i] | [nj i k a] | [nj i k o v] | [nj i k o v i] | [nj i k o m] | [nj i k o v i] | [nj i k o hh] | [nj i k m i] | [nj i k a m i] | {nu} | [n yy m i] | [nj i vdc i] | [i vdc k aa] | [nj i vdc k yy] ; 
define vExceptions [o d nj] | [o t nj] | [o vdt nj] | [n o s vdt] | [n o s tj] | {nan} | [n a nj] | {nal} | [nj e nj] | [nj e n] | [nj i c] | [sh e t k] | [n aa v a] ;
define tpExceptions [r VowelPhonem] | [l VowelPhonem] | [n VowelPhonem] | [nj VowelPhonem] | r .#. | l .#. | n .#. | [nj] .#. | {ner} | [r ConsonantPhonem] | [l ConsonantPhonem] | {vom} | [v ii m] | [v aa r] | [m VowelPhonem] | [v VowelPhonem .#.] ;
define sSuffix [lj ii m] | [lj ii m e] | [lj ii sh] | [lj ii tj e] | [lj ii] | [lj i a] | [nj ii k] | {ujem} | {ujeme} | [u j e sh] | {ujete} | {uje} | [u j uu] | [l VowelPhonem] | [lj VowelPhonem] | {mi} | {vetl} | [v e t lj] | {voj} ;
define hhExceptions {man} | [m a nj i] | {mana} | {manov} | {manovi} | [m a n o hh] | {manom} | {manmi} | {manami} | [nj a] | [nj e] | [nj ii] | [nj i a m] | [nj u] | [nj e] | [nj i a hh] | [nj a m i] | {no} | {ni} | [n uu] | {ron} ;
define cExceptions [nj i n] ; 
define tjExceptions [m i .#.] ;

define vInGroup [v] @-> [vlv] || ConsonantPhonem _ ConsonantPhonem ConsonantPhonem ,, [v] @-> [vlv] || ConsonantPhonem ConsonantPhonem _ ConsonantPhonem ,, [v] @-> [vlv] || \j & ConsonantPhonem ConsonantPhonem ConsonantPhonem _ ,, [t] @-> [vdt] || ConsonantPhonem _ ConsonantPhonem ConsonantPhonem ,, [t] @-> [vdt] || ConsonantPhonem ConsonantPhonem _ ConsonantPhonem ,, [t] @-> [vdt] || ConsonantPhonem ConsonantPhonem ConsonantPhonem _ ,, v @-> [vlv] || o _ [n a k] ,, v @-> [vlv] || [dj e] _ [e t] ; 
define voicingExceptions k @-> [vdk] || _ AdjectiveSuffix .#. | tpExceptions ,, t @-> [vdt] || .#. | .#. VowelPhonem | .#. GeneralPrefix | .#. GeneralPrefix VowelPhonem _ VoicedConsonantPhonem \.#. ,, t @-> [vdt] || _ tpExceptions | AdjectiveSuffix .#. | {vr} | {vl} ,, p @-> [vdp] || _ tpExceptions | AdjectiveSuffix .#. ,, p @-> [vdp] || .#. | .#. VowelPhonem | .#. GeneralPrefix | .#. GeneralPrefix VowelPhonem _ VoicedConsonantPhonem \.#. ,, s @-> [vds] || [p aa] | [p ii] | [p o] _ m ,, s @-> [vds] || [nj e] _ [m [[i e] | [i e m] | [i e sh] | [i e tj] | [uu] | [e l] | [e l a] | [e l ii]]] ,, s @-> [vds] || _ [m e l] | [m e r] ,, sh @-> [vdsh] || _ AdjectiveSuffix | sSuffix .#. ,, ts @-> [vdts] || _ AdjectiveSuffix | sSuffix .#. ,, s @-> [vds] || .#. sPrefix _ \{byt} ,, s @-> [vds] || _ sSuffix .#. | tpExceptions | [ConsonantPhonem r] | [ConsonantPhonem l] | [r ConsonantPhonem] | [l ConsonantPhonem] | VoicedConsonantPhonem VowelPhonem r .#. | [v e d] ,, c @-> [vdc] || _ AdjectiveSuffix .#. ,, t @-> [vdt] || {pe} _ r ,, f @-> [vdf] || _ tpExceptions | AdjectiveSuffix .#. ,, c @-> [vdc] || cExceptions ,, [k v] @-> [vdk vlv] || _ .#. | VowelPhonem ,, k @-> [vdk] || _ [r ConsonantPhonem] | [l ConsonantPhonem] | [r vdt] | [l vdt] | [r vdp] | [l vdp] ,, tj @-> [vdtj] || _ tjExceptions ; 
# [hh] @-> [vdhh] || .#. | .#. VowelPhonem | .#. GeneralPrefix | .#. GeneralPrefix VowelPhonem _ VoicedConsonantPhonem \.#. ,, [hh] @-> [vdhh] || _ l ,, [hh] @-> [vdhh] || {te} _ ,, [hh] @-> [vdhh] || _ hhExceptions ,, 
define voicing p @-> [vlb] || \.#. _ VoicedConsonantPhonem ,, tj @-> [vldj] || \.#. _ VoicedConsonantPhonem ,, t @-> [vld] || \.#. _ VoicedConsonantPhonem ,, c @-> [vldz] || \.#. _ VoicedConsonantPhonem ,, ts @-> [vldzh] || \.#. _ VoicedConsonantPhonem ,, k @-> [vlg] || \.#. _ VoicedConsonantPhonem ,, sh @-> [vlzh] || \.#. _ VoicedConsonantPhonem ,, s @-> [vlz] || \.#. \LongVowelPhonem _ VoicedConsonantPhonem \LongVowelPhonem ,, s @-> [vlz] || .#. _ {me} .#. ,, f @-> [vlv] || \.#. _ VoicedConsonantPhonem ,, [ks] @-> [vlg vlz] || e _ {is} ,, p @-> pvlb || \.#. _ .#. ,, t @-> pvld || \.#. _ .#. ,, tj @-> pvldj || \.#. _ .#. ,, c @-> pvldz || \.#. _ .#. ,, ts @-> pvldzh || \.#. _ .#. ,, k @-> pvlg || \.#. _ .#. ,, hh @-> pvlh || \.#. _ .#. ,, s @-> pvlz || \.#. _ .#. ,, sh @-> pvlzh || \.#. _ .#. ,, f @-> pvlv || \.#. _ .#. ; 
# voicing hh to h is not very practical to do in the middle of a word, but at least leave it commented out here
# hh @-> [vlh] || \.#. _ VoicedConsonantPhonem ,, 
#define devoicingExceptions v @-> [vlv] || .#. | .#. VowelPhonem | .#. ConsonantPhonem | .#. [vdt] | .#. [vdp] | .#. ConsonantPhonem [vdp] | .#. ConsonantPhonem [vdt] | .#. GeneralPrefix | .#. GeneralPrefix VowelPhonem | .#. GeneralPrefix ConsonantPhonem ConsonantPhonem | .#. GeneralPrefix ConsonantPhonem [vdt] | .#. GeneralPrefix ConsonantPhonem [vdp] | .#. [vdt] | .#. GeneralPrefix [vdt] _ \VoicelessConsonantPhonem ,, v @-> [vlv] || VoicelessConsonantPhonem | b | d | g | j |l | m | n | r | [dz] | [dsh] | [vdc] _ .#. ,, v @-> [vlv] || _ VSuffix .#. | AdjectiveSuffix .#. | VowelPhonem .#. | VowelPhonem VowelPhonem .#. | VowelPhonem VoicelessConsonantPhonem ,, v @-> [vlv] || {no} _ {in} | [ii n] ; 
define devoicingExceptions v @-> [vlv] || .#. ConsonantPhonem _ ConsonantPhonem ,, v @-> [vlv] || .#. ConsonantPhonem VowelPhonem | .#. ConsonantPhonem ConsonantPhonem VowelPhonem _ \.#. ,, v @-> [vlv] || _ VSuffix .#. | AdjectiveSuffix .#. | vExceptions | VowelPhonem .#. | VowelPhonem VowelPhonem .#. | VowelPhonem VoicelessConsonantPhonem ,, v @-> [vlv] || {no} _ {in} | [ii n] ,, v @-> [vlv] || VoicelessConsonantPhonem | b | d | g | j |l | m | n | [dz] | [dsh] | [vdc] _ .#. ,, v @-> [vdf] || .#. a _ [sh a k] ; 
define devoicingF v @-> [vdf] || .#. | .#. GeneralPrefix _ VoicelessConsonantPhonem ; 
define devoicing b @-> [vdp] || _ VoicelessConsonantPhonem | .#. ,, dj @-> [vdtj] || _ VoicelessConsonantPhonem | .#. ,, dzh @-> [vdts] || _ VoicelessConsonantPhonem | .#. ,, dz @-> [vdc] || _ VoicelessConsonantPhonem | .#. ,, d @-> [vdt] || _ VoicelessConsonantPhonem | .#. ,, g @-> [vdk] || _ VoicelessConsonantPhonem | .#. ,, h @-> [vdhh] || _ VoicelessConsonantPhonem | .#. ,, zh @-> [vdsh] || _ VoicelessConsonantPhonem | .#. ,, z @-> [vlz] || .#. _ .#. ,, z @-> [vds] || \.#. _ VoicelessConsonantPhonem | .#. ,, v @-> [w] || \.#. _ \l & \r & \VowelPhonem | .#. ; 
define voicingDevoicing [vdp] @-> [p] || _ ,, [vdtj] @-> [tj] || _ ,, [vdts] @-> [ts] || _ ,, [vdc] @-> [c] || _ ,, [vdt] @-> [t] || _ ,, [vdk] @-> [k] || _ ,, [vdhh] @-> [hh] || _ ,, [vds] @-> [s] || _ ,, [vdsh] @-> [sh] || _ ,, [vdf] @-> [f] || _ ,, [w] @-> [u] || _ ,, [vlb] @-> [b] || _ ,, [vldj] @-> [dj] || _ ,, [vld] @-> [d] || _ ,, [vldz] @-> [dz] || _ ,, [vldzh] @-> [dzh] || _ ,, [vlg] @-> [g] || _ ,, [vlh] @-> [h] || _ ,, [vlzh] @-> [zh] || _ ,, [vlz] @-> [z] || _ ,, [vlv] @-> [v] || _ ; 

define Stage1 InitialTranscribe @-> ; 
define Stage2 Digraphs @-> ; 

define tgStress a0:a1|aa0:aa1|e0:e1|ee0:ee1|ije0:ije1|i0:i1|ii0:ii1|iii0:iii1|o0:o1|oo0:oo1|u0:u1|uu0:uu1|ou0:ou1|y0:y1|yy0:yy1|ax0:ax1;
define noStressV  a0 | aa0 | e0 | ee0 | ije0 | i0 | ii0 | iii0 | o0 | oo0 | uo0 | u0 | uu0 | ou0 | y0 | yy0 | ax0 ;
define stressV  a1 | aa1 | e1 | ee1 | ije1 | i1 | ii1 | iii1 | o1 | oo1 | uo1 | u1 | uu1 | ou1 | y1 | yy1 | ax1 ;
define vowel  noStressV  | stressV;
define pc   \vowel;
define pcx   pc - StressMark;
define noStress noStressV | pc | StressMark;

define vowelizeR  r -> [iii0 r]  || pc _ pcx ,,
ax0 -> iii0 || _ r ; 

define vowelizeL  l -> [iii0 l]  || pc _ pcx ,,
ax0 -> iii0 || _ l ; 


source stress.foma
source unicodechars.foma

regex Stage1 .o.
Stage2 .o.
spellSpecialCharacters .o. processSpecialCharacters .o. 
deExceptions .o. diiExceptions .o. diExceptions .o. 
neExceptions .o. niiExceptions .o. niExceptions .o. 
teExceptions .o. tiExceptions .o. tiiExceptions .o. 
rules @->  .o. exceptionsEnd .o. vInGroup .o. voicingExceptions .o. devoicingExceptions .o. voicing .o. devoicingF .o. devoicing .o. voicingDevoicing .o. 
UnicodeToNativeTranslit .o. 
procStressG2p .o.
procStress2 .o.
vowelizeR .o.
vowelizeL ;

