# Copyright (C) 2022 Darko Milosevic <daremc86@gmail.com>
# Copyright (C) 2023 Peter Vagner <pvagner@pvagner.sk>

# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU Lesser General Public License as published by
# the Free Software Foundation, either version 2.1 of the License, or
# (at your option) any later version.

# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Lesser General Public License for more details.

# You should have received a copy of the GNU Lesser General Public License
# along with this program.  If not, see <https://www.gnu.org/licenses/>.

define InitialTranscribe 
# Now, we should define some phonemes which represents something symillar as schwa for vocalize R and L
а:ax1 | 
и:iil1 | 
и:iir1 | 
a:a | 
ä:ae | 
á:aa | 
b:b | 
c:c | 
č:ts | 
d:d | 
[d z]:dz | 
ď:dj | 
e:e | 
é:ee | 
f:f | 
g:g | 
h:h | 
i:i | 
í:ii | 
j:j | 
k:k | 
l:l | 
ľ:lj | 
ĺ:ll | 
m:m | 
n:n | 
ň:nj | 
o:o | 
ó:oo | 
ô:uo | 
ö:o | 
p:p | 
q:kv | 
r:r | 
ř:rz | 
ŕ:rr | 
s:s | 
š:sh | 
ß:s | 
t:t | 
ť:tj | 
u:u | 
ú:uu | 
ů:uu | 
ü:u | 
v:v | 
x:[ks] | 
y:y | 
ý:yy | 
z:z | 
ž:zh ; 

define WTranscribe w:v @-> ; 

define Digraphs 
[c h]:[hh] | 
[d zh]:[dzh] | 
[z s]:[s] | 
[m ě]:[m nj e] | 
[d ě]:[dj e] | 
[t ě]:[tj e] | 
[n ě]:[nj e] | 
ě:[i e] ; 

define spellSpecialCharacters [
[ě]: [ee z m e k ts e nj o m] |
[ů]: [uu s k r uu sh k o m] 
] @-> || .#. _ .#. ; 

define ShortVowelPhonem [a | e | i | o | u | y | ae ] ; 
define LongVowelPhonem [aa | ee | ije | ii | oo | uo | uu | ou | yy | iil1 | iir1 | ax1 ] ; 
define VowelPhonem ShortVowelPhonem | LongVowelPhonem ; 
#define ConsonantPhonem [b|c|ts|d|dj|dz|dzh|f|g|h|hh|j|k|l|lj|m|n|nj|p|r|rz|s|sh|t|tj|v|z|zh|vdp|vdt] ; 
# Todo: should lj also be a voiced consonant for assimilation?
define VoicedConsonantPhonem [j|b|d|dj|dz|dzh|g|h|z|zh|v|m|n|nj|l|r] ; 
define VoicelessConsonantPhonem [p|t|tj|c|ts|k|hh|s|sh|f|vdp|vdt|vdtj|vdc|vdts|vdk|vdhh|vds|vdsh|vdf] ; 
define ConsonantPhonem [VoicedConsonantPhonem | VoicelessConsonantPhonem | rz] ; 

define viachlasException hh -> [c h] || .#. {via} _ {las} ;
define beginingDe {bat} | {ce} | [f i n i t ii v] | {inte} | {k} | {leg} | {lim} | [l t [a | aa | y | .#. | e | u | o]] | {mo} | {nis} | {nzi} | {pres} | {sig} | {sti} | {tail} | {tek} | [v ii z] | z ; 
# [k aa d] | {kel} | {kl} | {kor} | [k oo r] | {krement} | [k r ee t] | 
define dePrefix {akt} | {bil} | {but} | [c i \e] | [c i .#.] | {duk} | {fault} | {fic} | {fin} | {form} | {gene} | [g r e s ii v] | {gust} | {hydr} | [k oo d] | [l f ii n] | {ment} | {menc} | {militar} | {nomin} | [n t aa l] | [o ks y] | [oo n] | {pono} | {rby} | [r i v aa c] | [r i v aa t] | {rma} | [r m i t ii] | [r n i ee] | [sh p e k t] | [sh t r u] | {tegova} | {term} | [t o ks] | {ton} | {valv} | {vast} | {vel} ; 
define deExceptions {de} @-> [ŧde] || .#. _ beginingDe , _ dePrefix , {evi} | [k o r e sh p o n] | {kre} | {prezi} | {respon} | [sh t u] _ n , .#. {vi} _ a | [aa] | [ii] | o | u , {mo} _ l | m | r , .#. i _ a | [aa] | [ii] | u , i _ {nti} | o , .#. {fe} _ r | [ks] , {in} _ [ks] , [\v | .#.] i _ n , .#. {ka} _ t , [\sh a] | [.#. a] _ k , [\sh a] | [.#. a] _ p , {je} _ n .#. | [n aa] , [k a zh] | {mla} _ j , {po} | {pre} _ [nj] .#. , {bor} _ l , {aka} | {epi} _ m , {in} | [k oo] _ [ks] , {ten} _ {nc} | [n ts] , [zh i a] _ n .#. , {po} _ [ks p o] , [l ii] _ r , {oro} | u | l | r | [sh e] _ j .#. , [.#. a] _ l , {kon} _ {nz} , .#. z _ [n ShortVowelPhonem .#.] | {nk} | {nek} , a _ {nc} | {nt} , {ho} _ n .#. ; 
# Breaking divadelná: a _ l 
define deForeignExceptions [d e] @-> [ŧde] || [rz ?*] _ , _ [?* rz] , _ {sktop} ; 
define beginingDi [aa] | [ee] | {ec} | {et} | [f t ii n] | g | {men} | [n aa r] | p | s | [v ii z] | {zajn} ; 
define diPrefix {abet} | {agn} | {akriti} | {alekt} | {ck} | {dakti} | f | {kt} | {lem} | {menz} | {nosaur} | o | [oo] | r | {scip} | {sp} | {st} | {stic} | [sh p u t] | [sh t r i k] | {ureti} | {verz} | {zmus} ; 
define diPostfix .#. {au} | {expe} | {han} | {in} | {kre} | [.#. l yy] | {melo} | [m e l oo] | {meto} | [m ee] | {mo} | {or} | {skan} | [sh t a] | [sh t aa] | [sh t i p e n] | [sh t u] | [sh t uu] | {tra} ; 
define diExceptions [d i] @-> [ŧdi] || {ru} | {se} _ {ment} , [sh k a n] _ [n aa v] | {nav} , a | [[\d | .#.] e] | o | n _ [ts n VowelPhonem] , .#. _ beginingDi , _ diPrefix , {me} _ [[t \l] | [a \r] | [a .#.] | [r \[y]] | e | i | o | u | y | aa | ee | ije | ii | oo | uo | uu | ou | yy | j|b|d|dj|dz|dzh|g|h|z|zh|v|m|n|nj|l|p|tj|c|ts|k|hh|s|sh|f] , [aa] _ i .#. , [aa] _ a | [aa] | i | [ii] | o | u , {ra} _ [aa] | {us} | [ks] , diPostfix _ , {perio} | [sh t a n d a r] _ z , {man} _ t , {re} _ g , .#. [o | {po} | {pre} | {na}] _ [sh] , .#. o _ n | s , .#. e _ t , .#. {invali} _ t | z , {pre} _ k , o _ c | k | {zolo} | [ŧde o l] , _ k .#. , .#. {ra} _ [k aa l] , .#. i _ l | o , .#. {ko} _ {fi} , .#. _ a .#. | [a l [oo | yy]] , .#. _ [ts] .#. , {kan} _ d | z , .#. _ {va} .#. | {ve} .#. | [v aa m] .#. | {vou} .#. | {vami} .#. , {hen} _ {kep} , _ {sku} \.#. , .#. z _ {skre} , [t r a g ee] _ \.#. , {neo} _ [sh] , .#. _ {an} \[i | ii | e .#. | ee] , .#. _ {zert} [a | aa | u] , [oo] _ u | [aa] | a | [ii] , 
 {pe} _ {atr} | {ater} , [p ee] _ [a | e | u | ii | i | [aa m] | [aa hh] | {ou} | {ami}] .#. , o _ {gnor} , {kar} _ [n aa l] , {ra{ _ {us} ; 
define diForeignExceptions [d i] @-> [ŧdi] || [rz ?*] _ , _ [?* rz] ; 
define diiExceptions [d ii] @-> [ŧdii] || .#. {na} | o | {po} | {pre} _ d | [dj] , [.#. {expe}] | [.#. {tra}] _ , {mla} _ .#. , .#. {na} | .#. o | .#. {po} | .#. {pre} _ [s tj] .#. , [r aa] _ i , [uu] _ i , {kon} _ {ci} , _ {ler} , .#. {hr} _ .#. ; 
define neExceptions [n e] @-> [ŧne] || _ {gat} | [g aa c] | {nt} | {nc} | {oklas} | {ore} | [oo] | {rv} | {utr} | [ks t] , .#. _ [h e m i aa] , .#. {be} _ f , .#. {fo} | .#. {li} | .#. [p r aa z d] _ , g | t | v _ r , [a d m i n i s t r a t ii v] | {ge} _ , {mag} | [p e r s o n aa l] | {pyre} | [r e aa l] | [r uo z] _ , [sh p e c i aa l] | [t e hh] | [zh i a d] _ , e _ {rg} , .#. {re} _ s , {ba} | [t r ee] _ r , {me} _ [zh] , .#. _ [o p r ee n] , {pa} _ l , {riad} | [aa l] | [ii z] _ {ho} , {av} | [aa v] | {ed} | {na} | t | {ba} | {ca} | [hh a] | {riha} | {rha} | [aa h a] | [aa j a] | [ii j a] | {ka} | {la} | {ma} | {skapa} | {iepa} | {kopa} | {erpa} | {ypa} | [aa p a] | {ra} | [ii s a] | {ta} | {sa} | {za} | [ts a] | [lj a] | [nj a] | [sh a] | [zh a] | [tj a] | {iame} | {lme} | {lome} | {rome} | [ks] | [aa l e] _ j .#. , _ [ts k o] .#. , .#. o | {vi} _ n .#. , [zh i a r] _ , [t ii] _ [dzh] , [m yy t] _ , {miest} _ .#. , .#. | g | i | l | o | r | {tra} _ t .#. | [t y .#.] | [t a .#.] | [t u .#.] | {tom} | {tov} | [t u .#.] | {te} .#. | [t o hh] | {tmi} | {tami} , [ts] _ j .#. , [[\m & \l] e] _ j .#. , i _ j .#. , o _ j .#. , ConsonantPhonem _ j .#. , [ts i e r] _ , {va} _ j .#. , [t ii v] _ {mu} .#. , {vin} _ \.#. , {da} _ j .#. , [lj] _ j .#. , [a r m aa d] _ .#. , {oriad} _ {mu} , _ [p aa l [e | o | u | s | .#.]] , [uu d] | [uu r] _ {ho} , {mi} _ [r aa l] , [i n t e r] _ \.#. , [k r ee t] _ \.#. , .#. _ {uro} \[b | d | v] , [.#. p aa d] _ .#. , [o l aa r] | [p u l aa r] _ {ho} | {mu} , [p a t o g ee n] _ .#. , [ts ii t a] _ j .#. , {ko} _ {ktor} | {ktu} | {ktov} , [ii m] | [ii v] _ [h o .#.] , {kabi} _ t , {in} _ {ci} , {kyber} _ t, [zh aa] _ r , [aa l] | {iel} | [ii v] | [n] | {ier} | [aa r] | {st} | [uu t] | [sh t] | [sh t] | [aa v] | [ee z] | [aa zh] | [ii z] _ [h o .#.] | [m u .#.] , [sh t aa t] _ \n , {to} _ [aa l] , _ [o g ee n] , {re} _ {san} , [t e l e v ii z] _ .#. , {ki} _ {mati} , p _ {um} , {ki} _ t , {sy} _ {rg} , {kala} _ {tik} , _ [k t aa r] , _ {apol} , {mi} _ {ral} , m _ {mo} , .#. _ [u r aa l] ; 
# reconsidered správne sociálne rule [s o c i aa l] | [s p r aa v] _ , 
define neForeignExceptions [n e] @-> [ŧne] || .#. k _ {dlo} .#. , [rz ?*] _ , _ [?* rz] ; 
define niExceptions [n i] @-> ŧni || [g ee] | {mo} _ , {bota} | {endro} | {go} | [g oo] | {harmo} | [h a r m oo] _ , {kolo} | [k o l oo] | [l ii] | [m e hh a] | [t e hh] | [t r ee] | {tro} _ , _ {kot} , .#. {tu} _ s , a _ m , {fo} _ c , [hh] _ c | k , {orga} _ c | z , {ro} _ c | k , {te} _ s , i _ {ci} , {mu} _ k | s , {tago} _ s , .#. {ge} | {imu} | {mi} _ , o _ k | s , {admi} _ s , _ {zmus} .#. , .#. [uu] _ a | e | i | o | u | [ii] , {defi} _ t , [f ee] _ [ks] , [j uu] _ , [aa] _ a | e | {ou} | u , [aa r] | {atki} | {iad} | [ii sh k i] _ .#. , .#. {va} _ l , _ {hilist} , {fi} _ c | t | [ts] , {ma} _ {fe} , {mu} _ [ts] , {tur} _ {ket} , [hh r o] _ z , [ts i e r] _ , [d aa v] _ , {kog} _ , {homoge} _ , {se} _ {or} , {hygie} _ , {vulka} _ {ck} , {ma} _ {pul} , {pa} _ k | {ck} , {huma} _ t | z , {mag} _ [t uu d] , [f i d ee r] | [s i o n aa l] _ .#. , .#. _ [g ee r] , {ze} _ t , [b r i t aa] _ , {biz} _ s , {se} _ l , {huma} _ zm , [m oo d] _ .#. , {galva} _ c | z , {bo} | {perso} _ {fik} , {amerika} | {vulka} | [m o ŧde r] _ z , u _ {ce} | {fi} | {fo} | {jn} | [k aa t] | [k oo] | l | {on} | [oo n] | {par} | s | t | {va} | {ver} | [ks] , {kli} _ (c) k , {komu} _ t , {cy} | {et} | {fre} | {ge} | {hym} | {iko} | {kano} | {lako} | {rako} | {so} | {to} | {ume} _ {ck} , {afrika} | {ago} | {alba} | {amerika} | {alpi} | {arme} | {balka} | {germa} | {hispa} | {japa} | {religio} | {roma} | {slova} | {slove} | {ugrofi} | {urba} _ s | z , .#. _ {tro} , {karbo} _ c | z , [.#. s ee] _ u , {eter} _ t , [.#. ŧde] _ s ;
# removed spálni rule [aa l] _ , 
define niForeignExceptions [n i] @-> [ŧni] || [rz ?*] _ , _ [?* rz] ; 
define niiExceptions [n ii] @-> [ŧnii] || [ŧde r] _ .#. , {mu} _ c , {vole} _ , [ŧde f i] _ c , {buz} | {ent} | {er} | {ere} | {ert} | {il} | {op} | {ot} | [ts a s] | [ts e] _ .#. , {ek} | [e ts] | {in} | {patr} | {tat} | [u ts] _ .#. , .#. i _ .#. , {atki} _ .#. , .#. o _ .#. , {der} | {pred} _ .#. , {odne} _ .#. , {kog} _ , [e lj] _ .#. , [a ts] _ .#. , {ved} _ .#. , [t o ts] _ .#. , {moc} _ .#. , [p o l o ts] _ .#. , {kro} _ k , .#. _ [l .#.] | [l u .#.] | [l e .#.] | [l o m .#.] , [a ŧdi ts] _ .#. , [[\b & \c & \r & \t & \v] o v a] _ , {fareb} _ .#. , [\ll zh e] _ , [aa m o r] | [aa r o d] _ .#. , {odda} | {smut} | [sh i k o v] _ .#. , [s m ae d] _ , {hlad} _ .#. ; 
# Controvertial rule {ave} _ for word nastavení
define niiForeignExceptions [n ii] @-> [ŧnii] || [rz ?*] _ \.#. , _ [?* rz] ; 
define teExceptions [t e] @-> [ŧte] || [b a hh] | {es} | {en} | {fil} | {gis} | {ia} | [i ŧni s] | {katas} | {lis} | {mas} | {mis} | {pe} | {se} | {sve} _ r , _ {nis} | [r a j sh] | {restri} | [ks] , {syn} | {zavia} _ , .#. _ [d a j sh] | [hh] | {lec} | {lef} | {leg} | {lek} | {lem} | {leo} | {lep} | {ler} | {lesk} | {lesen} | {leter} | {lev} | {mp} | {nd} | {nto} | o | [oo] | {rap} | {ras} | [r ee n] | [r ee z] | [r ts] | {rez} , [e ks i s] _ n , {fo} | {ho} | {kura} | .#. {nu} | {kar} _ l , {arak} | {arac} _ r , .#. {in} _ l | n , {kompe} _ n , _ {mati} , {ma} _ {ria} , {ma} _ [r i aa] , {stra} _ g , {ia} | [n aa s] _ .#. , {me} _ r .#. , v _ [d a j sh] , {de} _ [k t ii v] , {in} _ {rn} , {al} _ r , .#. {dein} | {pas} | [v a l] _ , [a r hh i] _ k , .#. {pa} _ {nt} , {sep} _ {mb} , [ee] _ r , {asis} _ n , [v sh e] _ [ts] , [ŧde] _ k , _ {ho} .#. | s .#. , .#. {ka} _ d | g , _ {raz} , {ka} _ [hh] , {al} _ [r n a t ii v] , _ {rm} | {st} , {arak} _ r , [e ks] _ r , .#. {tele} _ r , .#. {po} _ n , {in} | {li} _ r , {sys} _ m , {al} _ {rn} , v _ {dy} , {la} _ {nc} | {nt} | [n ts] , .#. _ [n .#.] | [n t o .#.] , _ {da} .#. , _ {mer} , {tam} _ n , {to} _ m | n , _ {jto} .#. , {me} _ o , .#. _ l .#. , {in} _ g , {ve} _ {rin} , .#. _ {lk} | {rk} , _ [hh n] | [hh ŧni] | [hh ŧne] , {ve} _ [r aa n] , _ {lenov} , _ [n ŧde n c] | [n ŧde n ts] , o _ {rap} , _ [s l [a | u | e | y | ou | [o u]]] , _ [h e r aa n] , _ [ŧni s] , {sa} _ {lit} , _ [a t] | [aa t] , {najin} _ [n z ii v] , _ {rorist} | {roriz} | {rory} | {roru} | {rorov} | {rorom} | {rore} | [r o r o hh] | {rormi} | [r o r .#.] , .#. _ [r n o .#.] | [r n aa] | [r n a .#.] | [r n .#.] | [r n u .#.] | [r n e .#. ] | {rnom} | {rnami} , [s k uu] _ r , [k r aa] _ r , {konzis} _ {nt} | {nc} | [n ts] , [ŧde] _ {gova} , {bak} _ {ri} , [p e ŧdi a] _ r , .#. | {avre} | {boha} | {esi} | {ia} | {knu} | {le} | [lj a] | [n aa s] | {pnu} | [ae] | {zla} _ j .#. , [zh i] | {nu} | [ts u] | [hh a] | {pa} | {my} | {da} | {ija} | {ka} | {ina} | [ts n a] _ j .#. , {ra} | {sa} | {ta} | [ts a] | [nj a] | {bde} | [zh a] | {pe} | [\.#. t r e] _ j .#. , {pre} | {zre} | {hni} | {pi} | {vi} | [sh i] | {by} | {ky} _ j .#. , {kry} | {ri} | {vr} | {ies} | [zh l] | {am} | {en} | [oo n] | {is} | {os} | {du} | [aa] | [sh] | {as} _ j .#. , .#. _ {dy} .#. , {pro} _ [o | ii | t] \.#. , _ [[ts o v a] | [ts u j] | [ts uu v a]] , {su} _ [r ee n] , [sh i e s] _ .#. , {hypo} _ , {repe} _ , {ezo} _ {ri} , {es} _ {ti} , [u r ts i] _ j , _ {lef} , {pro} _ {kt} , {in} _ [n z ii v] , {re} _ {nc} | [n ts] , {fila} _ {li} , {pa} _ {tik} | {tick} , {au} _ {nti} , {la} _ [r aa l] , .#. _ {nzi} , _ {nzo} , .#. s _ {re} , [e sh] _ [b aa] , {majs} _ r , s _ {p} [[o v] | {uj}] , e _ [r ŧni t] , .#. {ams} _ l | r ; 
define teForeignExceptions [t e] @-> [ŧte] || [rz ?*] _ , _ [?* rz] ; 
define tikapart1 {pa} | {ma} | {sta} | [s ŧte] | {tal} | {man} | {men} | {ia} | {uva} | [ŧne] | [ŧte n] | [p a ŧte] | {robo} | {go} ; 
define tikapart2 {bio} | {ero} | [s hh o l a s] | {plas} | {mas} | {nas} | {ges} | {ornamen} | [ŧni s] | {polonis} | {gnos} | {kaus} | {ru} | {nau} | {peu} | {pranos} ;
define tikaending [k a .#.] | [k y .#.] | [k e .#.] | [k aa m .#.] | [k u .#.] | [k o u .#.] | [k a m i .#.] | [k aa hh .#.] | [a .#.] | [e .#.] | [ii .#.] | [u .#.] | [aa m .#.] | [u .#.] | [i .#.] | [aa hh .#.] | [o u .#.] | [a m i .#.] ;
define tiExceptions [t i] @-> ŧti|| _ k .#. | {ng} .#. , _ {tul} , {abs} | {apa} | {atlan} _ , {automa} | {cer} | [ŧdi a g n o s] _ \.#. , {didak} | [ŧdi d a k] | {ek} | [e ks o] | {ema} | {farmaceu} _ , [f o ŧne] | {herme} | [hh a r a k ŧte r i s] | {ins} | [i n sh] | {inves} | {kon} _ , [k o n sh] | {kozme} | {kul} | [m a g ŧne] | [m a ŧte m a] | {mul} _ , {niko} | {produk} | {rk} _ , {sensi} | [s y n ŧte] | [sh t a] | [sh t a t i s] _ , [ŧte m a] | {ver} _ , {komple} | {konkre} | {kri} _ z , a _ z , {li} | n | {tas} | [\k y] _ c , {ama} | {ak} | {ep} | [hh a] | [hh a o] | {kri} | {mo} _ c | [k VowelPhonem] , \hh _ {ck} , {sa} _ r | s , {pres} _ [zh] , s _ {mul} , .#. _ {motej} | {ran} , .#. [r e sh] _ , [t e ks] _ l , .#. {pu} _ k , {ven} _ l , {ma} _ k , e _ k , e _ {ck} , _ {zme} .#. | {zmom} .#. | {zmu} .#. | {zmus} .#. | {vizme} .#. | {vizmom} .#. | {vizmu} .#. | {vizmus} .#. , {en} _ n , a _ {ck} , .#. _ {tan} , [sh] _ {pen} , {ak} | {mo} _ v , n _ n , {an} _ [k aa] , {mar} _ n , {ahnu} | {ihnu} _ .#. , .#. _ e .#. | {eto} .#. , .#. _ p .#. | {py} .#. | {pu} .#. | {pov} | {po} | {pe} .#. | {pmi} .#. , {kris} _ [aa] , {pe} _ [ts n] , _ {mea} | {mei} | [m e ii] | [m e aa m] | [m e u] | [m e aa hh] | {meou} | {meami} , .#. {an} _ , _ {ger} | {gr} , {fes} _ {val} , .#. {des} _ , .#. {jus} _ , .#. {kons} _ , .#. [a n ŧti p a] _ , {poli} _ c | k , .#. {ak} _ , {cen} _ \ShortVowelPhonem , {empa} _ , .#. {in} _ m , e _ [oo p] , {marke} _ {ng} , [ŧde s] _ l , s _ {gm} .#. , .#. _ [n [ou | [o u] | a | y | e | u]] .#. , [ŧde s] _ [n aa c i] , [ŧdi a k r i] _ , {kompa} _ {bil} , [ŧdi u r e] _ , .#. _ {bor} , {poli} _ [ts] , {aser} _ v , [\z i ŧde n] _ , {ar} _ {kul} , {uris} _ k , {eklip} _ k , _ {bet} , {mo} | {kri} _ [ts k] | {km} , {aris} _ k , [ŧde n] _ {fik} , {prak} _ {zuj} | {zov} , {fantas} _ {ka} | {ky} | {ke} | {ku} | [k aa] , {no} _ {fik} , {ges} _ {kul} , {bas} _ [aa n] , {absolu} _ {zm} , [ŧde n] _ t , {kvan} _ f , {akus} | [g e ŧne] _ k , {repe} _ , {fe} _ sh , _ {ramis} , {op} | {par} _ \ts & \l , [p r o ŧte] _ k | c , {narko} _ c | k , [ŧte ks] _ l , _ {cizm} , {aske} | {be} | {drama} | [.#. e] | {ero} | [e s ŧte] | {fana} | {fili} | {fone} | {forma} | {ge} | [g e ŧne] | {gma} | {gnos} _ [ts k [a | y | e | u | [o u] | [aa m] | [aa hh] | [a m i]] .#.] | [ts i e k .#.] , {kep} | {kulo} | {lek} | {lep} | {ly} | {man} | {mio} | {mis} | {mizma} | {noe} _ [ts k [a | y | e | u | [o u] | [aa m] | [aa hh] | [a m i]] .#.] | [ts i e k .#.] , o | {opa} | [.#. o p] | {ore} | [o ŧte] | {reuma} | {rne} | {ro} | {sta} | {tak} | {tma} | {ylis} _ [ts k [a | y | e | u | [o u] | [aa m] | [aa hh] | [a m i]] .#.] | [ts i e k .#.] , s _ {gma} , {balis} _ \e , {ra} _ {fik} , {vy} _ {pov} , {pija} _ c | k , {is} _ [k a .#.] | [k y .#.] | [k e .#.] | [k aa m] | [k u .#.] | [k aa hh] | {kou} | {kami} , _ {ptop} | [p u j] , 
tikapart1 _ tikaending , 
tikapart2 _ tikaending , 
{bas} | {flo} | [.#. o] | {tor} _ {la} | {ly} | {le} | [l aa m] | {lu} | {le} | [l aa hh] | {lou} , {ar} _ s , _ [f i k aa] | {fiko} | {fiku} , s _ [oo n] , {ul} _ [m aa t] , {krea} _ {vi} , {spas} _ c ; 
define tiExceptions2 [t i] @-> ŧti|| {ra} _ {fik} , {pale} | {poli} _ z ; 
define tiForeignExceptions [t i] @-> [ŧti] || [rz ?*] _ , _ [?* rz] ; 
define tiiExceptions [t ii] @-> [ŧtii] || {preven} _ v , {detek} | {dif} | {ek} | {sensi} | {vyknu} _ , {teris} _ k , _ {mov} , a | {ak} | {alterna} | {audi} | {defini} | i | [k o n sh t r u k] | {mo} | {nega} | {produk} | {spek} | [t u] _ v , .#. {jus} | {pales} _ , {or} _ m , {ales} _ t , .#. _ [m .#.] | {mo} | {mu} , {hnu} | {li} _ .#. , .#. _ .#. | {to} .#. , {kris} _ n , _ [ŧne dzh] , {pe} _ {ci} , [ŧni] _ , _ {mea} | {mei} | [m e ii] | [m e aa m] | [m e u] | [m e aa hh] | {meou} | {meami} , .#. _ m , {legi} _ m , {adap} _ v , .#. {in} _ m , [ŧde ŧte k] _ v , {argen} _ n , [hh a r a k ŧte r i s] _ k , [z zh i] _ , {aser} _ v , [ks e k u] _ , {antark} _ d , [k o n sh] _ {nopo} , [ŧde n] _ t , .#. {boha} _ .#. , [p r e ŧdi k] _ v , {poli} _ k , {fik} _ v , {opi} | {pripi} | {prepi} | {spi} | {spros} _ .#. , {repe} _ , [k o n sh ŧti t u] _ v , {is} _ k .#. , tikapart1 _ [k .#.] , tikapart2 _ [k .#.] , {flo} | {tor} _ l , {par} _ c , {ape} _ t , [ŧde sh t r u k] _ v ; 
# These tii rules are questionable: {os} _ k , breaks podujatí {ja} _ .#. , 
define rules [d i]:[dj i] | [t i]:[tj i] | [n i]:[nj i] | [l ii]:[lj ii] | [l i]:[lj i] | [d e]:[dj e] | [t e]:[tj e] | [n e]:[nj e] | [l e]:[lj e] | [d ii]:[dj ii] | [n ii]:[nj ii] | [t ii]:[tj ii] ; 
define exceptionsEnd [ŧde] @-> [d e] || _ ,, [ŧdii] @-> [d ii] || _ ,, [ŧdi] @-> [d i] || _ ,, [ŧne] @-> [n e] || _ ,, [ŧnii] @-> [n ii] || _ ,, [ŧni] @-> [n i] || _ ,, [ŧte] @-> [t e] || _ ,, [ŧtii] @-> [t ii] || _ ,, [ŧti] @-> [t i] || _ ; 

define GeneralPrefix {bez} | {do} | [d uo] | [m i m o] | {na} | {nad} | [n aa] | [nj e] | [nj e n a] | o | {od} | {odo} | {po} | [vdp o] | {pol} | [vdp o l] | {pre} | [vdp r e] | {pred} | [vdp r e d] | {pri} | [vdp r i] | {roz} | s | {so} | [sh] | {tri} | [vdt r i] | u | {uza} | v | {vy} | {vyna} | {za} | [z aa] ; 
define sPrefix {popri} | [vdp o vdp r i] | {pre} | [vdp r e] | {pri} | [vdp r i] | {za} | [z aa] ;
define AdjectiveSuffix {na} | [n aa] | {ne} | [nj e] | {ny} | [n yy] | {ni} | [nj i] | [n ii] | [nj ii] | {neho} | [nj e h o] | [y hh] | {nemu} | [nj e m u] | [n ee m u] | {nym} | {no} | {nom} | [n yy m] | {nimi} | [nj i m i] | [n ii m i] | [nj ii m i] | {nej} | [nj e j] | [n ee] | [n ee h o] | {mi} | [nj ii k] | [nj ii vdk] | [n uu tj] | [n y hh] | [n yy hh] | [yy mi] | [nj o v] | [n o s tj] | [n o s tj i] | [n o s tj ii] | [n o s tj i a m] | [n o s tj i a hh] | [n o s tj ou] | [n o s tj a m i] | [n uu] ;
define VSuffix [o v a tj] | [a tj] | [aa tj] | {ujem} | {ujeme} | [u j e sh] | [u j e tj e] | {oval} | {ovala} | {ovalo} | {ujeme} | [u j e tj e] | [u j uu] | {ame} | {am} | [a sh] | [a tj e] | a | {ame} | [a tj e] | [a j uu] | {aj} | [o r i tj] | {orka} | {il} | [i lj i] | {ila} | {ilo} | {ina} | {inka} | {iny} | {inky} | [ii n] | [i nj i e k] | [i nj e] | {inke} | [i n aa m] | [i n k aa m] | {inu} | {inku} | {iny} | {inky} | [i nj e] | {inke} | [i n aa hh] | [i n k aa hh] | {inou} | {inkou} | {inami} | {inkami} | {ice} | {iciam} | [i c i a hh] | {icami} | [ii c] | [nj ii k y] | [nj ii k a] | [nj ii k o v] | [v nj ii k u] | [nj ii k o m] | [nj ii k o hh] | [nj ii k a m i] | [nj ii k m i] | [nj ii k] | [nj ii vdk] | {la} | {ala} | [nj a] | {ov} | [a n yy] | [a n aa] | [a n ee] | [a n ii] | [a n ee h o] | {anej} | [a nj e j] | [a n ee m u] | [a n yy m] | [a n uu] | [a n yy hh] | {anom} | [a n yy hh] | [a n yy m i] | [nj u j e m] | [nj u j e sh] | [nj u j e] | [nj u j e m e] | [nj u j e tj e] | [nj u j uu] | {ner} | {neri} | {nera} | {nerov} | {nerovi} | {nerom} | {nera} | [n e r o hh] | {nerami} {nermi} | {na} | [nj i k] | [nj i c i] | [nj i k a] | [nj i k o v] | [nj i k o v i] | [nj i k o m] | [nj i k o v i] | [nj i k o hh] | [nj i k m i] | [nj i k a m i] | {nu} | [n yy m i] | [nj i vdc i] | [i vdc k aa] | [nj i vdc k yy] ; 
define vExceptions [o d nj] | [o t nj] | [o vdt nj] | [n o s vdt] | [n o s tj] | {nan} | [n a nj] | {nal} | [nj e nj] | [nj e n] | [nj i c] | [sh e t k] | [n aa v a] ; 
define tpExceptions [r VowelPhonem] | [l VowelPhonem] | [n VowelPhonem] | [nj VowelPhonem] | r .#. | l .#. | n .#. | [nj] .#. | {ner} | [r ConsonantPhonem] | [l ConsonantPhonem] | {vom} | [v ii m] | [v aa r] | [m VowelPhonem] | [v VowelPhonem .#.] ; 
define sSuffix [lj ii m] | [lj ii m e] | [lj ii sh] | [lj ii tj e] | [lj ii] | [lj i a] | [nj ii k] | {ujem} | {ujeme} | [u j e sh] | {ujete} | {uje} | [u j uu] | [l VowelPhonem] | [lj VowelPhonem] | {mi} | {vetl} | [v e t lj] | {voj} ; 
define hhExceptions {man} | [m a nj i] | {mana} | {manov} | {manovi} | [m a n o hh] | {manom} | {manmi} | {manami} | [nj a] | [nj e] | [nj ii] | [nj i a m] | [nj u] | [nj e] | [nj i a hh] | [nj a m i] | {no} | {ni} | [n uu] | {ron} ; 
define cExceptions [nj i n] ; 
define tjExceptions [m i .#.] ; 

define vInGroup [v] @-> [vlv] || ConsonantPhonem _ ConsonantPhonem ConsonantPhonem ,, [v] @-> [vlv] || ConsonantPhonem ConsonantPhonem _ ConsonantPhonem ,, [v] @-> [vlv] || \j & ConsonantPhonem ConsonantPhonem ConsonantPhonem _ ,, [t] @-> [vdt] || ConsonantPhonem _ ConsonantPhonem ConsonantPhonem ,, [t] @-> [vdt] || ConsonantPhonem ConsonantPhonem _ ConsonantPhonem ,, [t] @-> [vdt] || ConsonantPhonem ConsonantPhonem ConsonantPhonem _ ,, v @-> [vlv] || o _ [n a k] ,, v @-> [vlv] || [dj e] _ [e t] ; 
define voicingExceptions k @-> [vdk] || _ AdjectiveSuffix .#. | tpExceptions ,, k @-> [vdk] || \.#. _ [r vlv] ,, t @-> [vdt] || .#. | .#. VowelPhonem | .#. GeneralPrefix | .#. GeneralPrefix VowelPhonem _ VoicedConsonantPhonem \.#. ,, t @-> [vdt] || _ tpExceptions | AdjectiveSuffix .#. | {vr} | {vl} ,, p @-> [vdp] || _ tpExceptions | AdjectiveSuffix .#. ,, p @-> [vdp] || .#. | .#. VowelPhonem | .#. GeneralPrefix | .#. GeneralPrefix VowelPhonem _ VoicedConsonantPhonem \.#. ,, sh @-> [vdsh] || _ AdjectiveSuffix | sSuffix .#. ,, ts @-> [vdts] || _ [r vdt] | [r t] | AdjectiveSuffix | sSuffix .#. | [n a t VowelPhonem] | l | r ,, ts @-> [vdts] || [uu] | [i e] _ n ,, ts @-> [vdts] || a | [k r] _ m ,, c @-> [vdc] || _ AdjectiveSuffix .#. ,, t @-> [vdt] || {pe} _ r ,, f @-> [vdf] || _ tpExceptions | AdjectiveSuffix .#. ,, c @-> [vdc] || cExceptions ,, [k v] @-> [vdk vlv] || _ .#. | VowelPhonem ,, k @-> [vdk] || _ [r ConsonantPhonem] | [l ConsonantPhonem] | [r vdt] | [l vdt] | [r vdp] | [l vdp] ,, tj @-> [vdtj] || _ tjExceptions ,, [s m] @-> [vlz m] || .#. | .#. {ko} | .#. {no} | .#. {pro} _ e .#. ,, [ks] @-> [vdk vds] || [s e] _ {is} ; 
# [hh] @-> [vdhh] || .#. | .#. VowelPhonem | .#. GeneralPrefix | .#. GeneralPrefix VowelPhonem _ VoicedConsonantPhonem \.#. ,, [hh] @-> [vdhh] || _ l ,, [hh] @-> [vdhh] || {te} _ ,, [hh] @-> [vdhh] || _ hhExceptions ,, 
define blockVoicing [s n] @-> [vds n] || _ ,, [s nj] @-> [vds nj] || _ ,, [sh l] @-> [vdsh l] || _ ,, [sh lj] @-> [vdsh lj] || _ ,, [s l] @-> [vds l] || _ ,, [s lj] @-> [vds lj] || _ ,, [sh r] @-> [vdsh r] || _ ,, [s r] @-> [vds r] || _ ,, [s v] @-> [vds vlv] || _ ,, [sh v] @-> [vdsh vlv] || _ ,, [t v] @-> [vdt vlv] || _ ,, [t vlv] @-> [vdt vlv] || _ ,, [s m] @-> [vds m] || _ ,, [sh m] @-> [vdsh m] || _ ,, [sh n] @-> [vdsh n] || _ ,, [sh nj] @-> [vdsh nj] || _ , [ts nj] @-> [vdts nj] || _ , [v nj] @-> [vlv nj] || _ ; 
define voicing p @-> [vlb] || \.#. _ VoicedConsonantPhonem ,, tj @-> [vldj] || \.#. _ VoicedConsonantPhonem ,, t @-> [vld] || \.#. _ VoicedConsonantPhonem ,, c @-> [vldz] || \.#. _ VoicedConsonantPhonem ,, ts @-> [vldzh] || \.#. _ VoicedConsonantPhonem ,, k @-> [vlg] || \.#. _ VoicedConsonantPhonem ,, sh @-> [vlzh] || \.#. _ VoicedConsonantPhonem ,, s @-> [vlz] || \.#. \LongVowelPhonem _ VoicedConsonantPhonem \LongVowelPhonem ,, s @-> [vlz] || .#. _ {me} .#. ,, f @-> [vlv] || \.#. _ VoicedConsonantPhonem ,, [ks] @-> [vlg vlz] || e _ {is} | {eku} | [e k uu] ,, p @-> pvlb || \.#. _ .#. ,, t @-> pvld || \.#. _ .#. ,, tj @-> pvldj || \.#. _ .#. ,, c @-> pvldz || \.#. _ .#. ,, ts @-> pvldzh || \.#. _ .#. ,, k @-> pvlg || \.#. _ .#. ,, hh @-> pvlh || \.#. _ .#. ,, s @-> pvlz || \.#. _ .#. ,, sh @-> pvlzh || \.#. _ .#. ,, f @-> pvlv || \.#. _ .#. ,, s @-> [vlz] || {re} _ [i e b] ; 
# voicing hh to h is not very practical to do in the middle of a word, but at least leave it commented out here
# hh @-> [vlh] || \.#. _ VoicedConsonantPhonem ,, 
define devoicingExceptions v @-> [vlv] || .#. ConsonantPhonem _ ConsonantPhonem ,, v @-> [vdf] || {zo} _ [sh e o b] ,, [v] @-> [ww] || [ s t a] _ b ,, v @-> [ww] || [ s t aa] _ k ,, v @-> [vlv] || .#. ConsonantPhonem VowelPhonem _ \VoicelessConsonantPhonem ,, v @-> [vlv] || _ VSuffix .#. | AdjectiveSuffix .#. | vExceptions | VowelPhonem .#. | VowelPhonem VowelPhonem .#. | VowelPhonem VoicelessConsonantPhonem ,, v @-> [vlv] || {no} _ {in} | [ii n] ,, v @-> [vlv] || VoicelessConsonantPhonem | b | d | g | j |l | m | n | [dz] | [dsh] | [vdc] _ .#. ,, v @-> [vdf] || .#. a _ [sh a k] ,, v @-> [vdf] || .#. do _ {ted} ,, [t v] @-> [vdt vdf] || .#. o _ {ted} ,, v @-> [ww] || o _ [vdc] | [ts] ,, v @-> [vlv] || VowelPhonem _ [j | l | m | n] VowelPhonem ; 
define devoicingF v @-> [vdf] || .#. | .#. GeneralPrefix | .#. GeneralPrefix GeneralPrefix _ VoicelessConsonantPhonem ,, v @-> [vdf] || j _ [sh] ; 
define devoicing b @-> [vdp] || _ VoicelessConsonantPhonem | .#. ,, dj @-> [vdtj] || _ VoicelessConsonantPhonem | .#. ,, dzh @-> [vdts] || _ VoicelessConsonantPhonem | .#. ,, dz @-> [vdc] || _ VoicelessConsonantPhonem | .#. ,, d @-> [vdt] || _ VoicelessConsonantPhonem | .#. ,, g @-> [vdk] || _ VoicelessConsonantPhonem | .#. ,, h @-> [vdhh] || _ VoicelessConsonantPhonem | .#. ,, zh @-> [vdsh] || _ VoicelessConsonantPhonem | .#. ,, z @-> [vlz] || .#. _ .#. ,, z @-> [vds] || \.#. _ VoicelessConsonantPhonem | .#. ,, v @-> [ww] || \.#. & \[vdt] _ \l & \r & \z & \VowelPhonem | .#. ; 
define devoicingV2 z @-> [vds] || _ [vdp | vdt | vdtj | vdk | vdhh] ,, zh @-> [vdsh] || _ [vdp | vdt | vdtj | vdk | vdhh] ; 
define voicingDevoicing [vdp] @-> [p] || _ ,, [vdtj] @-> [tj] || _ ,, [vdts] @-> [ts] || _ ,, [vdc] @-> [c] || _ ,, [vdt] @-> [t] || _ ,, [vdk] @-> [k] || _ ,, [vdhh] @-> [hh] || _ ,, [vds] @-> [s] || _ ,, [vdsh] @-> [sh] || _ ,, [vdf] @-> [f] || _ ,, [ww] @-> [u] || _ ,, [vlb] @-> [b] || _ ,, [vldj] @-> [dj] || _ ,, [vld] @-> [d] || _ ,, [vldz] @-> [dz] || _ ,, [vldzh] @-> [dzh] || _ ,, [vlg] @-> [g] || _ ,, [vlh] @-> [h] || _ ,, [vlzh] @-> [zh] || _ ,, [vlz] @-> [z] || _ ,, [vlv] @-> [v] || _ ; 

define Stage1 InitialTranscribe @-> ; 
define Stage2 Digraphs @-> ; 

define tgStress a0:a1|aa0:aa1|e0:e1|ee0:ee1|ije0:ije1|i0:i1|ii0:ii1|iil0:iil1|iir0:iir1|o0:o1|oo0:oo1|u0:u1|uu0:uu1|ou0:ou1|y0:y1|yy0:yy1|ax0:ax1;
define noStressV  a0 | aa0 | e0 | ee0 | ije0 | i0 | ii0 | iil0 | iir0 | o0 | oo0 | uo0 | u0 | uu0 | ou0 | y0 | yy0 | ax0 ;
define stressV  a1 | aa1 | e1 | ee1 | ije1 | i1 | ii1 | iil1 | iir1 | o1 | oo1 | uo1 | u1 | uu1 | ou1 | y1 | yy1 | ax1 ;
define vowel  noStressV  | stressV;
define pc   \vowel;
define pcx   pc - StressMark;
define noStress noStressV | pc | StressMark ; 

define vowelizeR  r -> [iir0 r]  || pc _ pcx ,,
ax0 -> iir0 || _ r ; 

define vowelizeL  l -> [iil0 l]  || pc _ pcx ,,
ax0 -> iil0 || _ l ; 

define vowelizeRr  rr -> [iir0 rr]  || pc _ pcx ,,
ax0 -> iir0 || _ rr ; 

define vowelizeLl  ll -> [iil0 ll]  || pc _ pcx ,,
ax0 -> iil0 || _ ll ; 

source stress.foma

regex Stage1 .o.
Stage2 .o.
spellSpecialCharacters .o.
viachlasException .o.
deExceptions .o.
deForeignExceptions .o.
diiExceptions .o.
diExceptions .o.
diForeignExceptions .o. 
neExceptions .o.
neForeignExceptions .o.
niiExceptions .o.
niiForeignExceptions .o.
niExceptions .o.
niForeignExceptions .o.
teExceptions .o.
teForeignExceptions .o.
tiiExceptions .o.
tiExceptions .o.
tiExceptions2 .o.
tiForeignExceptions .o.
rules @->  .o.
exceptionsEnd .o.
vInGroup .o.
voicingExceptions .o.
devoicingExceptions .o.
devoicingF .o.
blockVoicing .o.
voicing .o.
devoicing .o.
devoicingV2 .o.
voicingDevoicing .o.
WTranscribe .o.
procStressG2p .o.
procStress2 .o.
vowelizeR .o.
vowelizeL .o.
vowelizeRr .o.
vowelizeLl ;

