# -*- mode: Python; indent-tabs-mode: t; tab-width: 4 -*-
# Copyright (C) 2012, 2014, 2018  Olga Yakovleva <yakovleva.o.v@gmail.com>

# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.

# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.

# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <https://www.gnu.org/licenses/>.

import os

Import("env","libcore")
local_env=env.Clone()
local_env["libversion"]="2.0.0"
local_env["liblevel"]=1
src=["audio.cpp", "file_playback_stream_impl.cpp", "playback_stream.cpp"]
if "libao" in local_env["audio_libs"]:
	src.append("libao.cpp")
	local_env.Append(CPPDEFINES=("WITH_LIBAO","1"))
	local_env.MergeFlags(["!pkg-config --cflags --libs ao"])
if "pulse" in local_env["audio_libs"]:
	src.append("pulse.cpp")
	local_env.Append(CPPDEFINES=("WITH_PULSE","1"))
	local_env.MergeFlags(["!pkg-config --cflags --libs libpulse-simple"])
if "portaudio" in local_env["audio_libs"]:
	src.append("portaudio.cpp")
	local_env.Append(CPPDEFINES=("WITH_PORTAUDIO","1"))
	local_env.MergeFlags(["!pkg-config --cflags --libs portaudio-2.0"])
local_env.Prepend(LIBS=local_env["libcore"])
libaudio=local_env.BuildLibrary(local_env["libaudio"],src)
local_env.Depends(libaudio,libcore)
if local_env["PLATFORM"]!="win32":
	local_env.InstallLibrary(libaudio)

Export("libaudio")
