apply plugin: 'com.android.application'

ext.major_version=1
ext.minor_version=5
ext.revision_number=12

ext.myConfigFile=file("../../../config/RHVoice.conf")
ext.myExtraCoreAssetsDir=new File(new File(new File(buildDir,"data"),"core"),"assets")

task addConfigFile(type: Copy) {
    from myConfigFile
    into myExtraCoreAssetsDir
}

android {
    compileSdkVersion myCompileSdkVersion

compileOptions {
sourceCompatibility JavaVersion.VERSION_1_8
  targetCompatibility JavaVersion.VERSION_1_8
}

    defaultConfig {
        minSdkVersion myMinSdkVersion
        targetSdkVersion myTargetSdkVersion
        applicationId "com.github.olga_yakovleva.rhvoice.android"
        versionCode = 10000 * major_version + 100 * minor_version + revision_number
        versionName "${major_version}.${minor_version}.${revision_number}"
        ndk{
            abiFilters 'armeabi-v7a', 'x86', 'arm64-v8a', 'x86_64'
        }
        externalNativeBuild {
            ndkBuild {
                arguments 'V=1'
            }
        }
    }

    sourceSets.main {
    assets.srcDir myExtraCoreAssetsDir
}

if(rootProject.signRelease||rootProject.signDebug) {
    signingConfigs {
        release {
            storeFile file(rootProject.property("RHVoice.keyStore"))
            storePassword rootProject.property("RHVoice.keyStorePassword")
            keyAlias rootProject.property("RHVoice.keyAlias")
            keyPassword rootProject.property("RHVoice.keyPassword")
        }
    }
}

externalNativeBuild {
    ndkBuild {
        path 'src/main/jni/Android.mk'
    }
}

buildTypes {
    release {
        if(rootProject.signRelease) {
            signingConfig signingConfigs.release
        }
        minifyEnabled true
        shrinkResources true
        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
    }
    debug {
        if(rootProject.signDebug) {
            signingConfig signingConfigs.release
        }
        minifyEnabled true
        shrinkResources true
        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

dependencies {
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.work:work-runtime:2.7.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'org.conscrypt:conscrypt-android:2.5.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    implementation "androidx.documentfile:documentfile:1.0.1"
    implementation 'com.google.guava:guava:30.1.1-android'
}

preBuild.dependsOn addConfigFile
